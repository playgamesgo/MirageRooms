plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.rikonardo.papermake' version '1.0.6'
}

group = 'me.playgamesgo'
version = '0.0.1'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "jitpack"
        url "https://jitpack.io"
    }
    maven {
        name = "codemc"
        url = "https://repo.codemc.org/repository/maven-public/"
    }
    maven {
        name = "extendedclip"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        name = "enginehub"
        url = "https://maven.enginehub.org/repo/"
    }
    maven {
        name = "phoenix"
        url = "https://nexus.phoenixdevt.fr/repository/maven-public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT"
    implementation "com.github.simplix-softworks:simplixstorage:3.2.6"
    implementation "dev.jorel:commandapi-bukkit-shade:9.2.0"
    compileOnly "dev.jorel:commandapi-annotations:9.2.0"
    annotationProcessor "dev.jorel:commandapi-annotations:9.2.0"
    compileOnly "me.clip:placeholderapi:2.11.3"
    compileOnly "com.sk89q.worldguard:worldguard-bukkit:7.0.7"
    compileOnly "com.sk89q.worldedit:worldedit-bukkit:7.2.17"
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    implementation "de.tr7zw:item-nbt-api:2.12.1"
    compileOnly "io.lumine:MythicLib-dist:1.6.2-SNAPSHOT"
    compileOnly "net.Indyuce:MMOItems-API:6.9.5-SNAPSHOT"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate 'de.leonhard.storage', 'me.playgamesgo.libs.simplixstorage'
    relocate 'dev.jorel.commandapi', 'me.playgamesgo.libs.commandapi'
    relocate("de.tr7zw.changeme.nbtapi", "me.playgamesgo.libs.nbtapi")
}